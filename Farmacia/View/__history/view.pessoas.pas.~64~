unit view.pessoas;

interface

uses
  Winapi.Windows, Winapi.Messages, System.SysUtils, System.Variants, System.Classes, Vcl.Graphics,
  Vcl.Controls, Vcl.Forms, Vcl.Dialogs, view.padrao, Data.DB, Vcl.Grids, Vcl.DBGrids, Vcl.StdCtrls,
  Vcl.ComCtrls, Vcl.ExtCtrls, Vcl.Mask, Vcl.DBCtrls, Controller.Interfaces, Controller.Controle;

type
  TPessoas = class(TfrmPadrao)
    lblCode: TLabel;
    lblName: TLabel;
    lblPostalCode: TLabel;
    lblPublicPlace: TLabel;
    edtID: TDBEdit;
    edtName: TDBEdit;
    edtPostalCode: TDBEdit;
    edtPublicPlace: TDBEdit;
    edtDistrict: TDBEdit;
    edtDocument: TDBEdit;
    edtPhone: TDBEdit;
    lblPhone: TLabel;
    lblDocument: TLabel;
    lblStateCity: TLabel;
    lblDistrict: TLabel;
    edtCity: TDBEdit;
    edtState: TDBEdit;
    procedure btnDeleteClick(Sender: TObject);
    procedure btnSaveClick(Sender: TObject);
    procedure FormCreate(Sender: TObject);
    procedure btnSearchClick(Sender: TObject);
    procedure btnNewClick(Sender: TObject);
    procedure btnCancelClick(Sender: TObject);
    procedure edtDocumentKeyPress(Sender: TObject; var Key: Char);
  private
    { Private declarations }
    FControle: iControle;
    procedure ListarTodos;
  public
    { Public declarations }
  end;

var
  Pessoas: TPessoas;

implementation

{$R *.dfm}

procedure TPessoas.btnCancelClick(Sender: TObject);
begin
  inherited;
   dsSearch.DataSet.Cancel;
end;

procedure TPessoas.btnDeleteClick(Sender: TObject);
begin
  inherited;
   FControle
     .Entidades
     .Pessoa
       .This
         .Id(dsSearch.DataSet.FieldByName('ID').AsInteger)
       .&End
     .Excluir;
   ListarTodos;
end;

procedure TPessoas.btnNewClick(Sender: TObject);
begin
  inherited;
   ListarTodos;
   dsSearch.DataSet.Insert;
   if pcMain.ActivePageIndex = 0 then
      pcMain.ActivePageIndex := 1;
   edtName.SetFocus;
end;

procedure TPessoas.btnSaveClick(Sender: TObject);
var
   vPessoa: Integer;
begin
  inherited;
   if dsSearch.DataSet.FieldByName('NOME').AsString.Trim.IsEmpty then
   begin
      MessageDlg('Informe o Nome!', mtWarning, [mbOK], 0);
      edtName.SetFocus;
      Exit;
   end;

   if dsSearch.DataSet.State = dsInsert then
   begin
      FControle
        .Entidades
        .Pessoa
          .This
            .Nome(dsSearch.DataSet.FieldByName('NOME').AsString)
            .Telefone(dsSearch.DataSet.FieldByName('TELEFONE').AsString)
            .Logradouro(dsSearch.DataSet.FieldByName('LOGRADOURO').AsString)
            .CEP(dsSearch.DataSet.FieldByName('CEP').AsString)
            .Bairro(dsSearch.DataSet.FieldByName('BAIRRO').AsString)
            .Cidade(dsSearch.DataSet.FieldByName('CIDADE').AsString)
            .Estado(dsSearch.DataSet.FieldByName('ESTADO').AsString)
            .Tipo(dsSearch.DataSet.FieldByName('TIPO').AsString)
            .Documento(dsSearch.DataSet.FieldByName('DOCUMENTO').AsString)
          .&End
        .Inserir;
      ListarTodos;
      dsSearch.DataSet.Last;
   end
   else
   begin
      vPessoa := dsSearch.DataSet.FieldByName('ID').AsInteger;
      FControle
        .Entidades
        .Pessoa
          .This
            .Id(dsSearch.DataSet.FieldByName('ID').AsInteger)
            .Nome(dsSearch.DataSet.FieldByName('NOME').AsString)
            .Telefone(dsSearch.DataSet.FieldByName('TELEFONE').AsString)
            .Logradouro(dsSearch.DataSet.FieldByName('LOGRADOURO').AsString)
            .CEP(dsSearch.DataSet.FieldByName('CEP').AsString)
            .Bairro(dsSearch.DataSet.FieldByName('BAIRRO').AsString)
            .Cidade(dsSearch.DataSet.FieldByName('CIDADE').AsString)
            .Estado(dsSearch.DataSet.FieldByName('ESTADO').AsString)
            .Tipo(dsSearch.DataSet.FieldByName('TIPO').AsString)
            .Documento(dsSearch.DataSet.FieldByName('DOCUMENTO').AsString)
          .&End
        .Atualizar;
      ListarTodos;
      dsSearch.DataSet.Locate('ID', VarArrayOf([vPessoa]), []);
   end;
end;

procedure TPessoas.btnSearchClick(Sender: TObject);
begin
  inherited;
   ListarTodos;
end;

procedure TPessoas.edtDocumentKeyPress(Sender: TObject; var Key: Char);
begin
  inherited;
   if (Key in ['0'..'9'] = False) and
      (Word(Key) <> VK_BACK) then
      key := #0;
end;

procedure TPessoas.FormCreate(Sender: TObject);
begin
  inherited;
   FControle := TControle.New;
   ListarTodos;
end;

procedure TPessoas.ListarTodos;
begin
   FControle.Entidades.Pessoa.Listar.DataSet(dsSearch);
end;

end.
